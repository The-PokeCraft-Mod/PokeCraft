plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
    id "org.cadixdev.licenser" version '0.6.1' apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "org.cadixdev.licenser"
    apply plugin: "dev.architectury.loom"

    license {
        header = rootProject.file("HEADER")
        include("**/*.java")
    }

    tasks.checkLicenseMain {
        dependsOn(updateLicenses)
    }

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        mavenCentral()
        maven { url = "https://maven.thepokecraftmod.com/releases" }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-$rootProject.parchment_version@zip")
        }

        implementation("com.thepokecraftmod:modelLoader:1.2.2")
        implementation("com.thepokecraftmod:renderer:1.2.2")
        implementation("org.tukaani:xz:1.9")
        implementation("com.thebombzen:jxlatte:1.1.0")
    }

    publishing {
        publications {
            create(MavenPublication) {
                from(components["java"])
            }
        }
        repositories {
            mavenLocal()
            maven {
                name = "PokeModRepo"
                url = uri("https://maven.thepokecraftmod.com/releases")
                credentials {
                    username = project.properties["pokecraftLogin"]?.toString() ?: findProperty("REPO_LOGIN").toString()
                    password = project.properties["pokecraftToken"]?.toString() ?: findProperty("REPO_PASSWORD").toString()
                }
            }

        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        mavenLocal()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
